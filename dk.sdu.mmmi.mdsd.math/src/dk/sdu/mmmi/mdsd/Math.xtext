grammar dk.sdu.mmmi.mdsd.Math with org.eclipse.xtext.common.Terminals

generate math "http://www.sdu.dk/mmmi/mdsd/Math"


MathExp:
	'program' name=ID
	externals += External*
	variables += VarBinding*
;

External:
	{External} 'external' name=ID '(' params+=ID? (',' params+=ID)* ')'
;

VarBinding:
	'var' name=ID '=' expression=Exp
;

Exp returns Expression:
	Factor (( {Plus.left=current} '+' | {Minus.left=current}  '-' ) right=Factor)*
;

Factor returns Expression:
	Primary (({Mult.left=current} '*' | {Div.left=current}  '/'  ) right=Primary)*
;

Primary returns Expression:
	{MathNumber} value=INT |  VariableUse | LetBinding | ExternalUse | Parenthesis
;

Parenthesis returns Expression:
	{Parenthesis} '(' exp=Exp ')'
;

LetBinding:
	'let' name=ID '=' binding=Exp 'in' exp=Exp 'end'
;

Binding:
	VarBinding | LetBinding
;

VariableUse returns Expression:
	{VariableUse} ref = [Binding]
;

ExternalUse returns Expression:
	{ExternalUse} ref = [External] '(' exps+=Exp? (',' exps+=Exp)* ')'
;